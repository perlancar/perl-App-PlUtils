#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;
#use Log::Any '$log';

use App::PlUtils;
use File::Which;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{pllines} = {
    v => 1.1,
    summary => 'Count line numbers of Perl programs',
    description => <<'_',



_
    args => {
        file => $App::PlUtils::arg_file_multiple,
    },
    result_naked => 1,
};
sub pllines {
    require SourceCode::LineCounter::Perl;

    my %args = @_;

    my $files = $args{file};

    my $res = [];
    for my $file (@$files) {
        my $path = $file;
        if (!(-f $path) && $file !~ m!/!) {
            # search file in PATH
            $path = which($file);
        }
        next unless -f $path;

        my $rec = {file=>$file};
        push @$res, $rec;

        my $counter = SourceCode::LineCounter::Perl->new;
        $counter->count($path);
        $rec->{total}   = $counter->total;
        $rec->{code}    = $counter->code;
        $rec->{pod}     = $counter->documentation;
        $rec->{blank}   = $counter->blank;
        $rec->{comment} = $counter->comment;
    };

    $res;
}

Perinci::CmdLine::Any->new(
    url => '/main/pllines',
    read_config => 0,
    read_env => 0,
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

 % pllines dzil /home/ujang/bin/myscript


=head1 DESCRIPTION

This utility uses L<SourceCode::LineCounter::Perl> to count lines of
{code,POD,blank,comment} in a specified Perl script.


=head1 SEE ALSO

L<App::cloc> which uses the B<cloc> program and can output number of
{code,blank,comment} lines in a Perl source code (as well as other languages).
Results might differ slightly with result from this utility.

L<pmlines> (from L<App::PMUtils> distribution)
